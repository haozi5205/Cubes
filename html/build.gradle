apply plugin: "java"
apply plugin: "jetty"

gwt {
    gwtVersion='2.8.0' // Should match the gwt version used for building the gwt backend
    maxHeapSize="1G" // Default 256m is not enough for gwt compiler. GWT is HUNGRY
    minHeapSize="1G"

    src = files(file("src/")) // Needs to be in front of "modules" below.
    modules 'ethanjones.cubes.GdxDefinition'
    devModules 'ethanjones.cubes.GdxDefinitionSuperdev'
    project.webAppDirName = 'webapp'

    compiler {
        strict = true
        disableCastChecking = true
        setOptimize(0)
        saveSource = true
    }
}

task draftRun(type: JettyRunWar) {
    dependsOn draftWar
    dependsOn.remove('war')
    webApp=draftWar.archivePath
    daemon=true
}

task superDev(type: de.richsource.gradle.plugins.gwt.GwtSuperDev) {
    dependsOn draftRun
    doFirst {
    	gwt.modules = gwt.devModules
    }
}

task dist(dependsOn: [clean, compileGwt]) {
    doLast {
        file("build/dist").mkdirs()
        copy {
            from "build/gwt/out"
            into "build/dist"
        }
        copy {
            from "webapp"
            into "build/dist"
            }
        copy {
            from "war"
            into "build/dist"
        }
        copy {
            from "build/gwt/extra/html/src"
            from "build/gwt/extra/html/symbolMaps"
            into "build/dist/source"
        }

        delete "build/dist/index.html"
        delete "build/dist/WEB-INF"
        file("build/dist/play.html").renameTo(file("build/dist/index.html"))
        delete "build/dist/html/logo.png"
        copy {
            from ("../assets/assets/") {
                include "logo.png"
            }
            into "build/dist/html/"
        }
    }
}

task distZip(type: Zip) {
    from 'build/dist/'
    exclude 'dist.zip'
    archiveName 'dist.zip'
    destinationDir file('build/dist/')
}

draftWar {
   from "war"
}

task addSource << {
	sourceSets.main.compileClasspath += files(project(':core').sourceSets.main.allJava.srcDirs)
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)
tasks.draftCompileGwt.setStyle(de.richsource.gradle.plugins.gwt.Style.DETAILED)

sourceCompatibility = 1.6
sourceSets.main.java.srcDirs = [ "src/" ]


eclipse.project {
    name = appName + "-html"
}

publishing {
    publications {
        html(MavenPublication) {
            groupId 'ethanjones.cubes'
            artifactId 'html'
            version getMavenVersionString()
            artifact(new File(project.getBuildDir(), '/dist/dist.zip'))
        }
    }
    repositories {
        maven {
            url getMavenRepo()
            credentials {
                username localProperties.MAVEN_REPO_USERNAME
                password localProperties.MAVEN_REPO_PASSWORD
            }
        }
    }
}

distZip.dependsOn(['dist'])
publish.dependsOn(['distZip'])
